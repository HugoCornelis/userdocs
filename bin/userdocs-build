#!/usr/bin/perl -w 
#!/usr/bin/perl -w -d:ptkdb
#


use strict;


BEGIN
{
    #! make check

    push @INC, '../perl';
    #! make distcheck

    push @INC, '../../perl';

    #! normal run

    push @INC, './perl';

    #! after install

    push @INC, '/usr/local/glue/swig/perl';
}


use GENESIS3::Documentation;

use Getopt::Long;

use YAML;


my $option_parse_only = 0;
my $option_regex = ".*";
my $option_tags = [];
my $option_verbose;


sub main
{
    read_cmd_line();

    # get all documents from the command line

    my $documents
	= {
	   map
	   {
	       $_ => 1,
	   }
	   (
	    @ARGV
	    ? @ARGV
	    : @{ local $/ ; Load(`userdocs-tag-filter 2>&1 "published"`) },
	   ),
	  };

    # get all documents selected by tags

    foreach my $tag (@$option_tags)
    {
	local $/;

	my $documents_tag = Load(`userdocs-tag-filter 2>&1 "published" "$tag"`);

	if (!scalar @$documents_tag)
	{
	    next;
	}
	
	$documents
	    = {
	       %$documents,
	       map
	       {
		   $_ => $tag,
	       }
	       @$documents_tag,
	      };
    }

    # prepare directory structure

    `mkdir -p html/htdocs/neurospaces_project/userdocs/`;

    if (!scalar keys %$documents)
    {
	print "$0: no documents to build\n";
    }

    my $contents_documents
	= {
	   'contents-level1' => 1,
	   'contents-level2' => 1,
	   'contents-level3' => 1,
	   'contents-level4' => 1,
	   'contents-level5' => 1,
	   'contents-level6' => 1,
	   'contents-level7' => 1,
	  };

    foreach my $document_name (keys %$documents)
    {
	$document_name =~ s(/$)();

	$document_name =~ s(.*/)();

	chomp $document_name;

	if ($document_name !~ m/$option_regex/)
	{
	    next;
	}

	my $document
	    = GENESIS3::Documentation::Document->new
		(
		 {
		  name => $document_name,
		 },
		);

	# copy the document sources to the build directory

	my $copy_error = $document->copy( { verbose => $option_verbose, }, );

	if ($copy_error)
	{
	    print "$0: *** Error: for document $document_name: failed to copy ($copy_error)\n";
	}

	# expand document keywords

	if ($contents_documents->{$document_name})
	{
	    my $command = "userdocs-tag-replace-items $document_name $document_name/$document_name.tex --verbose";

	    system $command;

	    if ($?)
	    {
		print "$0: *** Error: for document $document_name: failed to execute ($command, $?)\n";
	    }
	}

	# build the document with the expansions

	my $build_error = $document->build( { verbose => $option_verbose, }, );

	if ($build_error)
	{
	    print "$0: *** Error: for document $document_name: failed to build ($build_error)\n";
	}

	# copy the result to the destination directory

	my $publish_error = $document->prepare_publish_document( { verbose => $option_verbose, }, );

	if ($publish_error)
	{
	    print "$0: *** Error: for document $document_name: failed to publish ($publish_error)\n";
	}

    }
}


sub read_cmd_line
{
    my $option_help = 0;
    my $option_version;

    my $result
	= GetOptions(
		     "help!" => \$option_help,
		     "parse-only!" => \$option_parse_only,
		     "regex=s" => \$option_regex,
		     "tags=s" => $option_tags,
		     "verbose+" => \$option_verbose,
		     "version" => \$option_version,
		    );

    if (!$result)
    {
	die "$0: *** Error: Error in option processing";
    }

    if ($option_version)
    {
    }

    # reporting options

    if ($option_help)
    {
	$0 =~ m(.*/(.*));

	my $program_name = $1;

	print
	    "

$program_name: build documentation and prepare them for web
publication.  Arguments on the command line are taken as documents
that need to be build.  If no arguments are given, all documents will
be build.

synopsis:
    $0 <document name1> <document name2> ...

options:
    --help            print usage information.
    --parse-only      only execute parse commands, without actually building documentation.
    --regex           selects documents by name (default is all).
    --tags            process the documents with these tags, multiple tags options may be given.
    --version         give version information.
    --v|verbose       tell what is being done, specify multiple times to get more feedback.

example usage:
    userdocs-copy documentation-homepage
    userdocs-build documentation-homepage
    firefox html/htdocs/neurospaces_project/userdocs/documentation-homepage/documentation-homepage.html

";

	exit 1;
    }

}


main();


